package hu.bme.mit.inf.kortargyalo.drones.simulation.model.queries

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://inf.mit.bme.hu/kortargyalo/dronesSimulation"
import "http://inf.mit.bme.hu/kortargyalo/dronesStructure"

private pattern roleFilled(r : Role){
	TaskInstance.roleInstances.allocatedDrone(t,_);
	TaskInstance.roleInstances.role(t,r);
}

private pattern notAllRoleFilled(t : TaskInstance){
	TaskInstance.roleInstances(t, r);
	neg find roleFilled(r);
}

pattern allRoleFilled(t : TaskInstance){
	neg find notAllRoleFilled(t);
}

//this warning is intentional
pattern crash(drone1 : DroneInstance, drone2: DroneInstance){
	DroneInstance.position(drone1,drone1position);
	Position.x(drone1position,drone1positionx);
	Position.y(drone1position,drone1positiony);
	Position.z(drone1position,drone1positionz);
	DroneInstance.drone.dronetype.dimension(drone1,drone1dimension);
	Dimension.width(drone1dimension, drone1dimensionx);
	Dimension.height(drone1dimension, drone1dimensiony);
	Dimension.depth(drone1dimension, drone1dimensionz);
	
	DroneInstance.position(drone2,drone2position);
	Position.x(drone2position,drone2positionx);
	Position.y(drone2position,drone2positiony);
	Position.z(drone2position,drone2positionz);
	DroneInstance.drone.dronetype.dimension(drone2,drone2dimension);
	Dimension.width(drone2dimension, drone2dimensionx);
	Dimension.depth(drone2dimension, drone2dimensionz);
	Dimension.height(drone2dimension, drone2dimensiony);

	check(CollisionHelper.doDronesCollide(drone1positionx,drone1positiony, drone1positionz, drone1dimensionx, drone1dimensiony, drone1dimensionz,drone2positionx, drone2positiony, drone2positionz,drone2dimensionx, drone2dimensiony, drone2dimensionz));
}

pattern leftScene(drone : DroneInstance){
	DroneInstance.position(drone, position);
	Position.x(position, x);
	Position.y(position, y);
	Position.z(position, z);
	DronesSimulation.droneInstances(ds, drone);
	
	DronesSimulation.scenario.allowedBounds.position(ds, pos);
	Position.x(pos, scenex);
	Position.y(pos, sceney);
	Position.z(pos, scenez);

	DronesSimulation.scenario.allowedBounds.dimension(ds, dim);
	Dimension.width(dim, boundx);
	Dimension.height(dim, boundy);
	Dimension.depth(dim, boundz);
	
	DronesSimulation.droneInstances(ds, drone);
	
	check(CollisionHelper.didDroneLeftScene(x,y,z, scenex, sceney, scenez, boundx, boundy, boundz));	
}

pattern collisionWithObject(drone : DroneInstance, object : Obstacle){
	DroneInstance.position(drone,droneposition);
	Position.x(droneposition,dronepositionx);
	Position.y(droneposition,dronepositiony);
	Position.z(droneposition,dronepositionz);
	DroneInstance.drone.dronetype.dimension(drone,dronedimension);
	Dimension.width(dronedimension, dronedimensionx);
	Dimension.height(dronedimension, dronedimensiony);
	Dimension.depth(dronedimension, dronedimensionz);	

	Obstacle.position.x(object, objectx);
	Obstacle.position.y(object, objecty);
	Obstacle.position.z(object, objectz);
	Obstacle.dimension.width(object, objectdimx);
	Obstacle.dimension.height(object, objectdimy);
	Obstacle.dimension.depth(object, objectdimz);
	
	check(CollisionHelper.doCollideWithObstacle(dronepositionx, dronepositiony, dronepositionz, dronedimensionx, dronedimensiony, dronedimensionz, objectx, objecty, objectz, objectdimx, objectdimy, objectdimz));
		
}